apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def isRunningOnTravis = System.getenv("CI") == "true"

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        buildTypes.each {
            if (isRunningOnTravis) {
                it.buildConfigField 'String', 'API_HOST', '"' + System.getenv("API_HOST") + '"'
                it.buildConfigField 'String', 'API_ACCOUNT', '"' + System.getenv("API_ACCOUNT") + '"'
                it.buildConfigField 'String', 'API_PASSWORD', '"' + System.getenv("API_PASSWORD") + '"'
                it.buildConfigField 'String', 'OAUTH_REQUEST_ID', ""
                it.buildConfigField 'String', 'OAUTH_IDENTIFIER', ""
            }else{
                it.buildConfigField 'String', 'API_HOST', API_HOST
                it.buildConfigField 'String', 'API_ACCOUNT', API_ACCOUNT
                it.buildConfigField 'String', 'API_PASSWORD', API_PASSWORD
                it.buildConfigField 'String', 'OAUTH_REQUEST_ID', OAUTH_REQUEST_ID
                it.buildConfigField 'String', 'OAUTH_IDENTIFIER', OAUTH_IDENTIFIER
            }

        }
    }
}

ext {

    bintrayRepo = 'maven'
    bintrayName = 'android-fm'

    publishedGroupId = 'com.joselopezrosario'
    libraryName = 'android-fm'
    artifact = 'android-fm'

    libraryDescription = ''

    siteUrl = 'https://github.com/joselopezrosario/filemaker-data-api'
    gitUrl = 'https://github.com/joselopezrosario/filemaker-data-api.git'

    libraryVersion = '0.0.3'

    developerId = 'joselopezrosario'
    developerName = 'Jose Lopez'
    developerEmail = 'joselopezrosario@gmail.com'

    licenseName = 'GNU General Public License v3.0'
    licenseUrl = 'https://github.com/joselopezrosario/filemaker-data-api/blob/master/LICENSE'
    allLicenses = ["GPL-3.0"]
}
group = publishedGroupId
version = libraryVersion

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId publishedGroupId
            artifactId artifact

            name libraryName
            description libraryDescription
            url siteUrl

            licenses {
                license {
                    name licenseName
                    url licenseUrl
                }
            }
            developers {
                developer {
                    id developerId
                    name developerName
                    email developerEmail
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {

    if (isRunningOnTravis) {
        bintray.user = System.getenv("BINTRAY_USER")
        bintray.key = System.getenv("BINTRAY_KEY")
    }else{
        bintray.user = BINTRAY_USER
        bintray.key = BINTRAY_KEY
    }

    bintray.configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        dryRun = false
        publish = true
        override = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
        }
    }
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.1'
}
